
import java.text.SimpleDateFormat

buildscript {
   repositories { 
      mavenLocal() 
      jcenter()
      mavenCentral()
   }
   dependencies {
      classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE")
   }
}

plugins { 
   id 'io.franzbecker.gradle-lombok' version '1.14'
   id "io.spring.dependency-management" version "1.0.4.RELEASE" 
   id "com.github.kt3k.coveralls" version "2.8.1" 
   id "org.owasp.dependencycheck" version "3.1.2"
   id "com.jfrog.bintray" version "1.7.3"
   id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'


jar {
   baseName = 'base-repo'
   version =  '0.1'
}

repositories {
   mavenLocal() 
   jcenter()
   mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
 
configurations {
   all*.exclude module : 'spring-boot-starter-logging'
}

dependencies {
   compile("org.springframework.boot:spring-boot-starter-data-rest")
   compile("org.javers:javers-spring-boot-starter-sql:3.9.0")
   compile("io.springfox:springfox-swagger-ui:2.9.0")
   
   compile("edu.kit.datamanager:service-base:0.1")
   //Commented for Travis Build
   //compile("edu.kit.datamanager:generic-message-consumer:0.1.0")

   compile("com.monitorjbl:spring-json-view:0.16")
   compile("org.postgresql:postgresql:42.2.1")
   compile("org.apache.tika:tika-core:1.18") 

   compileOnly('org.apache.httpcomponents:httpclient:4.5.6')
   
   //Java 11 Support 
   compile('javax.xml.bind:jaxb-api:2.2.11')
   compile('com.sun.xml.bind:jaxb-core:2.2.11')
   compile('com.sun.xml.bind:jaxb-impl:2.2.11')
   compile('javax.activation:activation:1.1.1')
   compile('org.javassist:javassist:3.23.1-GA')
    
   testCompile("com.h2database:h2:1.4.194") 
   testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
   testCompile("org.springframework.boot:spring-boot-starter-test")
   testCompile("org.springframework:spring-test")
   testCompile("org.springframework.security:spring-security-test")
   
    //Java 11 Support 
   testCompile("org.mockito:mockito-core:2.18.0")
   testCompile("org.powermock:powermock-module-junit4:2.0.0-beta.5")
   testCompile("org.powermock:powermock-api-mockito2:2.0.0-beta.5")

  
//   testCompile("org.mockito:mockito-core:2.20.0")
//   testCompile("org.powermock:powermock-module-junit4:1.7.4")
//   testCompile("org.powermock:powermock-api-mockito2:1.7.4")

   testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE')
   
   testCompile('org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2')
   asciidoctor('org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.2.RELEASE')
}

ext {
   snippetsDir = file("build/generated-snippets")
}

test { 
   outputs.dir snippetsDir
}


asciidoctor {
   attributes "snippets": snippetsDir,
            "version": version,
            "date": new SimpleDateFormat("yyyy-MM-dd").format(new Date())
   inputs.dir snippetsDir
   dependsOn test  
   sourceDir "docs/"
   outputDir "build/docs"
}

bootJar {
   manifest {
      attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
   }
   dependsOn asciidoctor
   from ("${asciidoctor.outputDir}/html5") {
      into 'static/docs'
   }
   launchScript()
}

lombok {
   version = '1.18.4'
   sha256 = ""
}

jacoco {
   toolVersion = "0.8.2"
}

jacocoTestReport {
   reports {
      xml.enabled true
      html.enabled true
   }}
